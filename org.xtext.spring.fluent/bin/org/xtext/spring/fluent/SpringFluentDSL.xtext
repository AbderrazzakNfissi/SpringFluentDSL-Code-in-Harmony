grammar org.xtext.spring.fluent.SpringFluentDSL with org.eclipse.xtext.common.Terminals

generate springFluentDSL "http://www.xtext.org/spring/fluent/SpringFluentDSL"

Model:
  prefix=Prefix
  appName=AppName
  dependencies=Dependencies
  entities=Entities;

Prefix:
  {Prefix} 'prefix:' prefix=STRING;

AppName:
  {AppName} 'app_name:' appName=STRING;

Dependencies:
  {Dependencies} 'dependencies' '{'
    dependencies+=Dependency (',' dependencies+=Dependency)*
  '}';

enum DependencyType:
  SpringDataJpa | SpringWeb | Lombok | MySQLDriver | DevTools 
  |SpringKafka | SpringIntegrationWS | SpringIntegrationWebFlux |
  SpringIntegrationSecurity | SpringIntegrationRedis | SpringIntegrationMongoDB
  | SpringIntegrationKafka | SpringIntegrationJPA | SpringIntegrationJDBC
  | SpringIntegrationHTTP | SpringIntegrationAMQP | SpringWebFlux |
  SpringWebServices | SpringValidation | SpringThymeleaf | SpringOAuth2Client
  | SpringJersey | SpringJDBC | SpringIntegration | SpringDataRest| SpringGraphQL
 | SpringDataElasticsearch | SpringDataJDBC | SpringDataMongoDB |
   Swagger | SpringSecurity | SpringActuator | SpringGraphQLTest; //to complete

Dependency:
  {Dependency} dependency=DependencyType;

Entities:
  {Entities} 'entities' '{'
    entities+=Entity*
  '}';

Entity:
  'entity' name=ID ('extends' '['
    'strategy' '=' strategy=Strategy ']'
    superType=[Entity|ID]
  )? '{'
    feature=Feature
  '}';

Feature:
  {Feature} operation=Operation id=IDENTITY  attributes+=(Attribute)* relations+=(Relation)*; 

Strategy :
	{Strategy} 
	 strategy=('SingleTable' | 'JoinedTable' | 'TablePerClass');
	
IDENTITY:
	'id' ':' type=Type 'pk' ;

Operation:
  'ops' ':' (
    operation=STRING
  );
  
Attribute:
  name=ID ':' type=Type;
 
 
enum Type:
  Long | Integer | Double | String | Date | Time | Boolean;
   

Relation:
  name=ID ':' target=[Entity|ID] type=('mto' | 'otm' | 'oto' | 'mtm');